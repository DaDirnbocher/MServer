
apply plugin: 'java'
apply plugin: 'application'
apply from: "${project.rootDir}/gradle/eclipse.gradle"

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'mServer.Main'
version = '0.0.1-SNAPSHOT'

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['test']
        }
    }
}

compileJava {
    options.compilerArgs = ['-Xlint:all']
}
compileTestJava {
    options.compilerArgs = ['-Xlint:all']
}

task updateVersion << {
    File propsFile = file('src/version.properties').absoluteFile
    Properties props = new Properties()
    props.load(propsFile.newDataInputStream())

    int nr = Integer.parseInt(props.getProperty('BUILD')) + 1
    String buildDate = new Date().format('dd.MM.yyyy HH:mm:ss')

	logger.lifecycle "==mserver======================"
	logger.lifecycle "Version: $version"
	logger.lifecycle "Number:  $nr"
	logger.lifecycle "Date:    $buildDate"
	logger.lifecycle "==mserver======================"
	
	props.setProperty('VERSION', project.version)
	props.setProperty('BUILD', "$nr")
	props.setProperty('DATE', buildDate)
	
	props.store(propsFile.newWriter(), null)
}

processResources.dependsOn updateVersion

repositories {
    maven {
          url "https://repo.mediathekview.de/repository/maven-public/"
    }
}

dependencies {
    compile project(':MSearch')
    compile 'commons-net:commons-net:3.5'
}
